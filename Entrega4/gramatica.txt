####################################
#  Traductores e Interpretadores   #
#             Entrega 2            #
#         Gramatica asociada       #
####################################
# Realizada por:                   #
# Wilmer Bandres 10-10055          #
# Gustavo El Khoury 10-10226       #
####################################

S               -> INST_PROGRAM Bloque_Inst | INST_PROGRAM Inst

Bloque_Inst     -> INST_BEGIN Lista_Inst INST_END
		  | INST_BEGIN Inst_Declare Lista_Inst INST_END
		  | INST_BEGIN Inst_Declare INST_END

Lista_Inst      -> Inst | Inst SEMICOLON Lista_Inst

Inst            -> Inst_Declare | Inst_Asignacion | Inst_Lectura | Inst_Salida |
                   Inst_If | Inst_Case | Inst_For | Inst_While | Inst_Funcion

Inst_Funcion    -> RTOI LPAREN Rango RPAREN 
		  | LENGTH LPAREN Rango RPAREN
		  | TOP LPAREN Rango RPAREN
		  | BOTTOM LPAREN Rango RPAREN 
		  | RTOI LPAREN VAR_IDENTIFIER RPAREN 
		  | LENGTH LPAREN VAR_IDENTIFIER RPAREN
		  | TOP LPAREN VAR_IDENTIFIER RPAREN
		  | BOTTOM LPAREN VAR_IDENTIFIER RPAREN'''

               
Inst_Declare    -> INST_DECLARE Lista_DeclareTipos

Lista_DeclareTipos   -> Lista_Declare
		        | Lista_Declare SEMICOLON Lista_DeclareTipos

Lista_Declare   -> Lista_Variables INST_AS Tipo

Tipo            -> TYPEDEF_INT | TYPEDEF_BOOL | TYPEDEF_RANGE

Lista_Variables -> VAR_IDENTIFIER | VAR_IDENTIFIER COMMA Lista_Variables

Inst_Asignacion -> VAR_IDENTIFIER EQUAL Expresion

Expresion       -> Operacion_booleana | Operacion_binaria | Rango

Inst_Lectura    -> INST_READ VAR_IDENTIFIER

Inst_Salida     -> INST_WRITE Lista_Aux
		  | INST_WRITELN  Lista_Aux

Lista_Aux       -> Expresion
		  | STRING 
		  | Expresion COMMA Lista_Aux
		  | STRING COMMA Lista_Aux


Inst_If         -> INST_IF Operacion_booleana INST_THEN Bloque_Control 
                   | INST_IF Operacion_booleana INST_THEN Bloque_Control INST_ELSE Bloque_Control

BloqueControl   -> Bloque_Inst | Inst

Inst_Case       -> INST_CASE Operacion_binaria INST_OF Casos INST_END

Casos           -> VAR_IDENTIFIER CASE_ASSIGN Bloque_Control SEMICOLON
		  | Rango CASE_ASSIGN Bloque_Control SEMICOLON
		  | VAR_IDENTIFIER CASE_ASSIGN Bloque_Control SEMICOLON Casos 
		  | Rango CASE_ASSIGN Bloque_Control SEMICOLON Casos

Inst_For        -> INST_FOR VAR_IDENTIFIER INST_IN Rango INST_DO Bloque_Control

Inst_While      -> INST_WHILE Operacion_booleana INST_DO Bloque_Inst 
		  | INST_WHILE Operacion_booleana INST_DO Inst

Operacion_booleana -> Operacion_binaria Opr_bool Operacion_binaria
		    | Operacion_booleana AND Operacion_booleana 
		    | Operacion_booleana OR Operacion_booleana 
		    | Operacion_binaria IN Rango
		    | LPAREN Operacion_booleana RPAREN
		    | Operacion_booleana EQEQ Operacion_booleana
		    | Operacion_booleana NEQEQ Operacion_booleana
		    | Operacion_binaria EQEQ Operacion_binaria
		    | Operacion_binaria NEQEQ Operacion_binaria
		    | TRUE
		    | FALSE
		    | VAR_IDENTIFIER
		    | NOT Operacion_booleana

Opr_bool        -> GREAT | LESS | GREATEQ | LESSEQ

Operacion_binaria -> Operacion_binaria PLUS Term
		    | Operacion_binaria MINUS Term
		    | Term

Term              -> Term TIMES Factor
		    | Term DIVIDE Factor
		    | Term MOD Factor
		    | Factor
		    
Factor            -> NUMBER
		    | VAR_IDENTIFIER
		    | LPAREN Operacion_binaria RPAREN
		    | Inst_Funcion
		    | MINUS Factor
		    
Rango             -> Operacion_binaria RANGE Operacion_binaria
		    | Rango PLUS Rango
		    | Rango TIMES Operacion_binaria
		    | Rango INTERSECTION Rango 
		    | LPAREN Rango RPAREN
		    | VAR_IDENTIFIER
